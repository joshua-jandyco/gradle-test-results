plugins {
  id "groovy"
  id "jacoco"
  id "maven-publish"
  id "nebula.integtest" version "3.0.3"
  id "com.gradle.plugin-publish" version "0.9.3"
}

group "com.jandyco"
description  "Prints the summary of run, failed and skipped tests at the end of the build"
version "1.0.0"

repositories {
    jcenter()
}

publishing {
    repositories {
        mavenLocal()
    }
    publications {
        plugin(MavenPublication) {
            from components.java
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Test Results Plugin')
                root.appendNode('description', project.description)
                root.appendNode('url', 'https://github.com/joshua-jandyco/gradle-test-results')
                root.appendNode('inceptionYear', '2016')

                def scm = root.appendNode("scm")
                scm.appendNode('url', 'https://github.com/joshua-jandyco/gradle-test-results')
                scm.appendNode('connection', 'scm:https://github.com/joshua-jandyco/gradle-test-results.git')
                scm.appendNode('developerConnection', 'scm:git://github.com/joshua-jandyco/gradle-test-results.git')

                def license = root.appendNode("licenses").appendNode("license")
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')
            }
        }
    }
}

dependencies {
    compile gradleApi()
    compile localGroovy()

    testCompile ("org.spockframework:spock-core:1.0-groovy-2.4") {
         exclude module: 'groovy-all'
    }
    testCompile 'cglib:cglib:3.2.0'
}

facets {
    funcTest {
        parentSourceSet = 'main'
        testTaskName = 'funcTest'
    }
}

jacoco {
    toolVersion = "0.7.5.201505241946"
}

jacocoTestReport {
    reports {
        html.enabled true
    }
}
